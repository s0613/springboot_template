plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.11'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
}

group = 'com.template'
version = '0.0.1-SNAPSHOT'
description = 'Spring Boot Service Template'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('testcontainersVersion', '1.19.3')
    set('resilience4jVersion', '2.1.0')
    set('bucket4jVersion', '8.7.0')
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // WebSocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.webjars:sockjs-client:1.5.1'
    implementation 'org.webjars:stomp-websocket:2.3.4'

    // Rate Limiting
    implementation "com.bucket4j:bucket4j-core:${bucket4jVersion}"
    implementation "com.bucket4j:bucket4j-redis:${bucket4jVersion}"
    implementation 'javax.cache:cache-api:1.1.1'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    // Circuit Breaker
    implementation "io.github.resilience4j:resilience4j-spring-boot3:${resilience4jVersion}"

    // Database
    implementation 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'

    // Hibernate JSONB Support
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.7.0'

    // Redis
    implementation 'redis.clients:jedis'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'
    runtimeOnly 'org.bouncycastle:bcpkix-jdk15on:1.70'

    // Google OAuth2 ID Token Verification
    implementation 'com.google.api-client:google-api-client:2.2.0'

    // Utilities
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // API Documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    // AWS S3
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.565'

    // AWS SES
    implementation 'com.amazonaws:aws-java-sdk-ses:1.12.565'

    // AWS SDK v2 for SNS
    implementation 'software.amazon.awssdk:sns:2.20.0'

    // NCP SENS SMS
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    implementation 'commons-codec:commons-codec:1.16.0'

    // PDF Generation with Flying Saucer
    implementation 'org.xhtmlrenderer:flying-saucer-pdf:9.1.22'
    implementation 'org.xhtmlrenderer:flying-saucer-core:9.1.22'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Test Data Builder
    testImplementation 'com.github.javafaker:javafaker:1.0.2'

    // MockWebServer for OAuth2 testing
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
    ignoreFailures = true
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}
